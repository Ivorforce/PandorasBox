buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "2.1.6.1-1.12"
def ENV = System.getenv()
if (ENV.DRONE_BUILD_NUMBER) {
    version += ".n" + ENV.DRONE_BUILD_NUMBER
}

group = "ivorius.pandorasbox" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "PandorasBox"

minecraft {
    version = "1.12-14.21.1.2387"
    runDir = "eclipse"
    mappings = "snapshot_20170629"
    useDepAts = true
}

dependencies {
    deobfCompile 'ivorius.ivtoolkit:IvToolkit:1.3.3-1.12'
    deobfCompile 'ivorius.mcopts:MCOpts:0.9.7.1'
}

shadowJar {
    exclude 'META-INF/*', 'META-INF/maven/**'
    relocate 'ivorius.mcopts', project.group + '.shadow.mcopts'
    classifier=''
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
